---
date:   2024-06-07
categories: infrastructure
tags: [ azure, dns, staticwebapps ]
header:
  image: "/assets/images/2024/06/2024-06-07-banner.png"
---

In my [last post]({% post_url 2024/06/2024-06-06-azure-dns %}), I registered a DNS domain name and set up Azure DNS for it.  I want my blog to use that domain name, so I need to set up a custom domain using Azure DNS.

<!-- more -->

Of course, nothing is that simple.  My blog uses repeatable deployments via [Azure Developer CLI](https://learn.microsoft.com/azure/developer/azure-developer-cli/overview), so there is a little bit of extra work to do.

If you are doing this as a one-off thing and not using repeatable deployments, then feel free to [follow the instructions in the documentation](https://learn.microsoft.com/azure/static-web-apps/custom-domain).  It will walk through doing this same thing in the Azure portal.  A lot of the work is done for you.

## Configuring the www domain

Let's start with configuring a subdomain.  I want `www.apps-on-azure.net` to point to the static web app.  This is a two part process that is done AFTER both the domain and the static web app are deployed:

1. Register the `www` subdomain as a [CNAME](https://en.wikipedia.org/wiki/CNAME_record) pointing to the static web app.
2. Create the `www.apps-on-azure.net` custom domain on the static web app.

I like to do these things in a [bicep module](https://learn.microsoft.com/azure/azure-resource-manager/bicep/modules).  It, for instance, allows me to easily add `blog.apps-on-azure.net` later on using exactly the same code.  Modularization is great!

Let's take a look at the addition to the `main.bicep file:

{% highlight bicep linenos %}
module wwwdomain 'modules/swa-custom-subdomain.bicep' = {
  name: 'www-custom-domain-${resourceToken}'
  scope: rg
  params: {
    name: 'www'
    zoneName: dnszone.outputs.name
    staticWebAppName: swa.outputs.name
  }
}
{% endhighlight %}

This is how you call any bicep module.  I've already used ready-made modules from the Azure Verified Modules collection.  This one will be my own module.  Note the use of `.outputs.name` here.  Each module can specify a set of outputs and these outputs can be used to define other resources or modules.  By putting the outputs references in this module definition, I ensure the deployments are sequenced properly so that the custom domain comes after both the DNS zone and the static web app is deployed.

Let's take a look at the module code:

{% highlight bicep linenos %}
targetScope = 'resourceGroup'

@description('The name of the subdomain')
param name string

@description('The host name of the static web app service.')
param staticWebAppName string

@description('The name of the Azure DNS hosted DNS zone')
param zoneName string

resource dnsZone 'Microsoft.Network/dnsZones@2018-05-01' existing = {
  name: zoneName
}

resource staticSite 'Microsoft.Web/staticSites@2023-12-01' existing = {
  name: staticWebAppName
}

resource cnameRecord 'Microsoft.Network/dnsZones/CNAME@2018-05-01' = {
  name: name
  parent: dnsZone
  properties: {
    TTL: 3600
    CNAMERecord: {
      cname: staticSite.properties.defaultHostname
    }
  }
}

resource customDomain 'Microsoft.Web/staticSites/customDomains@2023-01-01' = {
  name: '${name}.${zoneName}'
  parent: staticSite
  properties: {
    validationMethod: 'CNAME'
  }
}

output domainName string = cnameRecord.properties.fqdn
{% endhighlight %}

Let's walk through this:

* **Line 1** says this is resource group scoped.  In the `main.bicep`, I must include `scope: <some-resource-group>` when I define the parameters for the module.
* **Lines 3-10** define the properties I can pass to the module.  I use `@description()` so I know what each parameter is for.  This feeds Intellisense when using Visual Studio Code to write bicep.
* **Lines 12-17** define the existing resources - the DNS zone and the static web app.  This will ensure that they exist and allow me to access any properties of those resources later on.
* **Lines 19-28** create a CNAME record in my DNS zone.  This is required to create a custom domain based on a subdomain.
* **Lines 30-36** create a custom domain in my static web app that points to the subdomain.  It expects the subdomain to be defined via a CNAME.

The last check may fail because of timing.  DNS is wierd in that it needs time to propagate to other DNS servers.  This is especially true if the DNS server that the static web app uses has already cached the domain you are working on.

If it fails, then just retry the check within the Azure portal later on. Your infrastructure as code will still work in a completely new scenario, and subsequent deployments will work since the configuration of the custom domain is the same as what is defined.